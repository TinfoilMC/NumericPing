buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://www.jitpack.io' }
        maven { url 'https://files.minecraftforge.net/maven' }
        jcenter()
        maven { url 'https://www.dimdev.org/maven/' }
        maven { url 'https://repo.spongepowered.org/maven/' }
    }
    dependencies {
        classpath 'com.github.Chocohead:ForgeGradle:moderniser-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

plugins {
    id 'net.minecrell.licenser' version '0.4.1'
    id 'com.palantir.git-version' version '0.12.0-rc2'
}

apply plugin: 'net.minecraftforge.gradle.tweaker-client'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'java'

group 'com.github.creeper123123321.numericping'
version gitVersion()
archivesBaseName = 'NumericPing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://www.dimdev.org/maven/' }
    maven { url 'https://jitpack.io' }
    maven { url 'http://repo.strezz.org/artifactory/list/Strezz-Central' }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    implementation 'com.github.Chocohead:Rift:jitpack-SNAPSHOT:dev'
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            //exclude 'META-INF', 'META-INF/**'
            exclude 'us/myles/ViaVersion/SpongePlugin.class'
            exclude 'javassist/**'
        }
    }
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, 'mixins.numericping.refmap.json'
}

minecraft {
    version = '1.13.2'
    mappings = 'snapshot_20181130'
    runDir = 'run'
    tweakClass = 'org.dimdev.riftloader.launch.RiftLoaderClientTweaker'
}
